###########################################################################
#
# 
#
###########################################################################

import "Point.tea"
import "Polimino.tea"
import "util/lists.tea"





###########################################################################
#
# Generates all the free form polyominos of the given degree.
#
# Free polyominos are distinct as long as none is a translation,
# rotation or reflection of another.
#
###########################################################################

global polimino-generate-free ( degree ) {
    
    polimino-generate $degree $matches-free?
}





###########################################################################
#
# Generates all the one-sided form polyominos of the given degree.
#
# One-sided polyominos are distinct as long as none is a translation or
# rotation of another.
#
###########################################################################

global polimino-generate-one-sided ( degree ) {
    
    polimino-generate $degree $matches-one-sided?
}





###########################################################################
#
# Generates all polyominos of the given degree.
#
###########################################################################

define polimino-generate ( degree matches? ) {
    
    define seed [polimino ( 0 0 )]
    
    define allPolyominos [bread-polyominos $degree $seed () $matches?]

    map make-horizontal $allPolyominos
}





###########################################################################
#
# Returns the polyomino with the lowest height from the set composed of
# the given polyomino and its 90 degree rotation.
#
###########################################################################

define make-horizontal ( p ) {
    
    if { < [$p getMaxX] [$p getMaxY] } {
        polyomino-rotate-90 $p
    } {
        is $p
    }
}





###########################################################################
#
# The returned polyominos are not necessarily normalized.
#
# base - Polimino instance.
#
# root - Point instance. One of the points in the "base" Polimino.
#
###########################################################################

define OFFSET_LIST [map-apply point ( (1 0) (0 1) (-1 0) (0 -1) )]
        

define bread-polyominos ( degree base buffer matches? ) {
    
    if { > $degree [$base getDegree] } {
        define prevPointList [$base getPointList]
        define checkedPs     ()
        
        foreach root [$base getPointList] {
        
            foreach offset $OFFSET_LIST {
                define newPoint [$root translate $offset]
            
                if { $base contains? $newPoint } {
                    # Nothing to do.
                } {
                    define newPointList \
                            [prepend $newPoint $prevPointList]
                    define newBase      \
                            [apply polimino-from-points $newPointList]
                    
                    set! newBase [$newBase normalize]
                    
                    if { $matches? $newBase $checkedPs } {
                        # We already used this one as base.
                    } {
                        set! checkedPs [prepend $newBase $checkedPs]
                        set! buffer \
                                [bread-polyominos $degree $newBase $buffer $matches?]
                    }
                }
            }
        }
    } {
        if { $matches? $base $buffer } {
            # Already found
        } {
            set! buffer [prepend $base $buffer]
        }
    }
    
    is $buffer
}





###########################################################################
#
# 
#
###########################################################################

define matches-one-sided? ( polyomino pList ) {
    
    define workingP   $null
    define matchFound $false
    
    if { not $matchFound } {
        set! workingP   $polyomino
        set! matchFound [matches-exactly? $workingP $pList]
    }
    if { not $matchFound } {
        set! workingP   [polyomino-rotate-90 $workingP]
        set! matchFound [matches-exactly? $workingP $pList]
    }
    if { not $matchFound } {
        set! workingP   [polyomino-rotate-90 $workingP]
        set! matchFound [matches-exactly? $workingP $pList]
    }
    if { not $matchFound } {
        set! workingP   [polyomino-rotate-90 $workingP]
        set! matchFound [matches-exactly? $workingP $pList]
    }
    
    is $matchFound
}





###########################################################################
#
# 
#
###########################################################################

define matches-free? ( polyomino pList ) {
    
    define workingP   $null
    define matchFound [matches-one-sided? $polyomino $pList]
    
    if { not $matchFound } {
        set! workingP   [polyomino-reflection-h $polyomino]
        set! matchFound [matches-one-sided? $workingP $pList]
    }
    if { not $matchFound } {
        set! workingP   [polyomino-reflection-v $polyomino]
        set! matchFound [matches-one-sided? $workingP $pList]
    }
    
    is $matchFound
}





###########################################################################
#
# 
#
###########################################################################

define matches-exactly? ( polyomino pList ) {
    
    if { empty? $pList } {
        is $false
    } {
        if { $polyomino isSame? [car $pList] } {
            is $true
        } {
            matches-exactly? $polyomino [cdr $pList]
        }
    }
}





###########################################################################
#
# 
#
###########################################################################

define polyomino-rotate-90 ( polyomino ) {
    
    define newPointList ()
    
    foreach p [$polyomino getPointList] {
        define newPoint [point [$p get y] [- [$p get x]]]
        set! newPointList [prepend $newPoint $newPointList]
    }
    
    [apply polimino-from-points $newPointList] normalize
}





###########################################################################
#
# 
#
###########################################################################

define polyomino-reflection-h ( polyomino ) {
    
    define newPointList ()
    
    foreach p [$polyomino getPointList] {
        define newPoint [point [- [$p get x]] [$p get y]]
        set! newPointList [prepend $newPoint $newPointList]
    }
    
    [apply polimino-from-points $newPointList] normalize
}





###########################################################################
#
# 
#
###########################################################################

define polyomino-reflection-v ( polyomino ) {
    
    define newPointList ()
    
    foreach p [$polyomino getPointList] {
        define newPoint [point [$p get x] [- [$p get y]]]
        set! newPointList [prepend $newPoint $newPointList]
    }
    
    [apply polimino-from-points $newPointList] normalize
}





###########################################################################
#
# 
#
###########################################################################

